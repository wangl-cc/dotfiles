#!/usr/bin/env fish

# Constants
set -g STATE_DIR "$HOME/.local/state/tunnel"
set -g LOG_DIR "$STATE_DIR/logs"
set -g INFO_DIR "$STATE_DIR/info"

mkdir -p $INFO_DIR
mkdir -p $LOG_DIR

# Global variables
set -g TUNNEL_TYPE # 'ssh', 'cloudflared'
# SSH connection details
set -g SSH_REMOTE_HOST
set -g SSH_REMOTE_PORT
set -g SSH_ARGS
# Cloudflare details
set -g HOSTNAME
set -g LOCAL_PORT

# A info file is a simple file that contains information about the SSH tunnel
# used to restart a tunnel
#
# tunnel_type = ssh # or cloudflared
# # SSH related
# ssh_remote_host = localhost # remote host to connect to
# ssh_remote_port = 1234 # remote port to connect to
# ssh_args = -i ~/.ssh/id_rsa user@host # additional arguments for ssh
# # Cloudflare related
# cloudflared_hostname = tunnel.example.com # Hostname for Cloudflared

function get_info_file
    echo "$INFO_DIR/$LOCAL_PORT.conf"
end

function read_tunnel_info
    # clear variables
    set TUNNEL_TYPE
    set SSH_REMOTE_HOST
    set SSH_REMOTE_PORT
    set SSH_ARGS
    set HOSTNAME

    set info_file (get_info_file)
    test -f $info_file; or return 1
    while read -l line
        set ret (string match -r '^(\w+)=(.*)' $line)
        set key (string trim -- $ret[2])
        set value (string trim -- $ret[3])
        switch $key
            case tunnel_type
                set TUNNEL_TYPE $value
            case remote_host ssh_remote_host
                set SSH_REMOTE_HOST $value
            case remote_port ssh_remote_port
                set SSH_REMOTE_PORT $value
            case ssh_args
                set SSH_ARGS $value
            case hostname ssh_hostname cloudflared_hostname
                set HOSTNAME $value
            case '*'
                echo "Unknown key: $key=$value found in $info_file, ignoring" >&2
        end
    end <$info_file

    return 0
end

function save_variable
    set name $argv[1]
    set value $argv[2]
    if test -n "$value"
        echo "$name=$value"
    end
end

function write_tunnel_info
    set info_file (get_info_file); or return $status
    begin
        save_variable tunnel_type "$TUNNEL_TYPE"
        save_variable ssh_remote_host "$SSH_REMOTE_HOST"
        save_variable ssh_remote_port "$SSH_REMOTE_PORT"
        save_variable ssh_args "$SSH_ARGS"
        save_variable hostname "$HOSTNAME"
    end >$info_file
end

# Check if a port is available and set pid and command that is listening on it
set -g LISTEN_PID
set -g LISTEN_CMD
function check_port
    set LISTEN_PID
    set LISTEN_CMD
    if set info (lsof -nP -iTCP:$LOCAL_PORT -sTCP:LISTEN -Fpc)
        for line in $info
            switch $line
                case 'p*'
                    set LISTEN_PID (string replace -r '^p' '' $line)
                case 'c*'
                    set LISTEN_CMD (string replace -r '^c' '' $line)
            end
        end
    end
end

function port_status
    # Check if the port is already in use by another SSH tunnel or other process
    check_port
    if test -n "$LISTEN_PID"
        switch $LISTEN_CMD
            case ssh
                echo SSH
            case cloudflared
                echo CLOUDFLARE
            case '*'
                echo OCCUPIED
        end
    else
        echo IDLE
    end
end

function start_tunnel
    set -l log_path "$LOG_DIR/$LOCAL_PORT.log"
    switch $TUNNEL_TYPE
        case ssh
            set -l remote "$SSH_REMOTE_HOST:$SSH_REMOTE_PORT"
            nohup ssh -NL $LOCAL_PORT:$remote $HOSTNAME $SSH_ARGS 2>&1 >$log_path &
            echo "SSH tunnel started on local port $LOCAL_PORT to $HOSTNAME->$remote"
        case cloudflare
            nohup cloudflared access tcp --hostname $HOSTNAME \
                --url http://localhost:$LOCAL_PORT 2>&1 >$log_path &
            echo "Cloudflare tunnel started on local port $LOCAL_PORT to $HOSTNAME"
        case '*'
            echo "Invalid tunnel type"
    end >&2
end

# Command: start a new SSH tunnel
function cmd_start
    # Define arguments using fish_opt
    set -l options
    set -a options (fish_opt --short=n --long=hostname --required-val)
    set -a options (fish_opt --short=t --long=tunnel-type --required-val)
    set -a options (fish_opt --short=H --long=remote-host --required-val)
    set -a options (fish_opt --short=r --long=remote-port --required-val)
    set -a options (fish_opt --short=l --long=local-port --required-val)

    argparse -i $options -- $argv

    if set -q _flag_tunnel_type
        set TUNNEL_TYPE $_flag_tunnel_type
    else
        echo "Error: Tunnel type not specified. Use -t or --tunnel-type." >&2
        exit 1
    end

    if set -q _flag_hostname
        set HOSTNAME $_flag_hostname
    else
        echo "Error: hostname not specified. Use -n or --hostname." >&2
        exit 1
    end

    switch $TUNNEL_TYPE
        case ssh
            # Set default remote host to localhost if not specified
            if set -q _flag_remote_host
                set SSH_REMOTE_HOST $_flag_remote_host
            else
                set SSH_REMOTE_HOST localhost
            end

            # Check required parameters
            if set -q _flag_remote_port
                set SSH_REMOTE_PORT $_flag_remote_port
            else
                echo "Error: Remote port not specified. Use -r or --remote-port." >&2
                exit 1
            end

            # Set default local port if not specified
            if set -q _flag_local_port
                set LOCAL_PORT $_flag_local_port
            else
                set LOCAL_PORT $SSH_REMOTE_PORT
            end

            # Handle relative local port (starting with +)
            if string match -q "+*" $LOCAL_PORT
                set offset (string replace "+" "" $LOCAL_PORT)
                set LOCAL_PORT (math $SSH_REMOTE_PORT + $offset)
            end

            # Pass rest of the arguments to ssh
            set SSH_ARGS $argv
        case cloudflare
            if set -q _flag_local_port
                set LOCAL_PORT $_flag_local_port
            else
                echo "Error: Local port not specified. Use -l or --local-port." >&2
                exit 1
            end
        case '*'
            echo "Error: Unknown tunnel type." >&2
            exit 1
    end

    switch (port_status)
        case SSH
            echo "Port $LOCAL_PORT is already in use by another SSH tunnel"
            echo "Use 'tunnel stop $LOCAL_PORT' to stop it"
            exit 1
        case CLOUDFLARE
            echo "Port $LOCAL_PORT is already in use by another Cloudflare tunnel"
            echo "Use 'tunnel stop $LOCAL_PORT' to stop it"
            exit 1
        case OCCUPIED
            echo "Port $LOCAL_PORT is already in use by another process"
            echo "Use 'lsof -i :$LOCAL_PORT' to check which process is using it"
            exit 1
        case IDLE
            start_tunnel
            write_tunnel_info
            exit 0
    end
end

# Command: stop an existing SSH tunnel
function cmd_stop
    # Check for port
    if test (count $argv) -lt 1
        echo "Error: Local port not specified" >&2
        usage
        exit 1
    end

    for port in $argv
        set LOCAL_PORT $port

        switch (port_status)
            case SSH CLOUDFLARE
                kill $LISTEN_PID
                echo "Stopped tunnel on port $LOCAL_PORT"
            case OCCUPIED
                echo "Port $LOCAL_PORT is used but not a tunnel"
                exit 1
            case IDLE
                echo "Port $LOCAL_PORT is not in use"
                exit 1
        end >&2
    end
end

# Command: restart an existing tunnel
function cmd_restart
    # Check for port
    if test (count $argv) -lt 1
        echo "Error: Local port not specified" >&2
        usage
        return 1
    end
    for port in $argv
        set LOCAL_PORT $port

        if not read_tunnel_info
            echo "$LOCAL_PORT is not used by any tunnel"
            exit 1
        end

        switch (port_status)
            case SSH CLOUDFLARE
                kill $LISTEN_PID
                echo "Stopped existing tunnel on port $LOCAL_PORT"
                start_tunnel
            case OCCUPIED
                echo "Port $LOCAL_PORT is already in use"
                exit 1
            case IDLE
                start_tunnel
        end >&2
    end
end

function cmd_status --description "Subcommand to show status of a SSH tunnel listening on given port"
    # Check for port
    if test (count $argv) -lt 1
        echo "Error: Local port not specified" >&2
        return 1
    end
    set LOCAL_PORT $argv[1]

    if not read_tunnel_info
        echo "$LOCAL_PORT is not used by any tunnel"
        exit 1
    end

    set log_path $LOG_DIR/$LOCAL_PORT.log

    switch (port_status)
        case SSH
            echo "SSH tunnel using local port $LOCAL_PORT is running (PID: $LISTEN_PID)"
            echo "Remote: $HOSTNAME -> $SSH_REMOTE_PORT:$SSH_REMOTE_PORT"
            test -n "$SSH_ARGS"; and echo "SSH arguments: $SSH_ARGS"
            if test -f $log_path; and test -s $log_path
                echo ---------------------- Log ----------------------
                cat $log_path
                echo -------------------------------------------------
            end
        case CLOUDFLARE
            echo "Cloudflare tunnel using local port $LOCAL_PORT is running (PID: $LISTEN_PID)"
            echo "Remote: $HOSTNAME"
            if test -f $log_path; and test -s $log_path
                echo ---------------------- Log ----------------------
                cat $log_path
                echo -------------------------------------------------
            end
        case OCCUPIED
            echo "Port $LOCAL_PORT is used by another process ($LISTEN_PID $LISTEN_CMD)"
        case IDLE
            echo "Tunnel on port $LOCAL_PORT is not running"
            echo "Last connection was to: $SSH_REMOTE_PORT:$SSH_REMOTE_PORT"
            echo "SSH arguments: $SSH_ARGS"
            echo "Use 'tunnel restart $LOCAL_PORT' to restart this tunnel."
    end >&2
end

# Command: list all tunnels
function cmd_list
    # List all info files
    set info_files (ls $STATE_DIR/info)

    if test -z "$info_files"
        echo "No tunnels found."
        exit 0
    end

    set FMT "%-5s %-10s %-5s %s\n"

    printf $FMT PORT STATUS PID REMOTE_INFO
    echo -----------------------------------------------------------------------

    for info_file in $info_files
        # info file should be in the format " <port >.conf"
        set LOCAL_PORT (string replace ".conf" "" $info_file)
        read_tunnel_info
        set remote_info
        switch $TUNNEL_TYPE
            case ssh
                set remote_info "$HOSTNAME->$SSH_REMOTE_HOST:$SSH_REMOTE_PORT"
            case '*'
                set remote_info $HOSTNAME
        end
        printf $FMT $LOCAL_PORT (port_status) "$LISTEN_PID" "$remote_info"
    end
end

# Function to show usage information
function usage
    begin
        echo "Usage: tunnel COMMAND [OPTIONS]"
        echo
        echo "Commands:"
        echo "  start     Start a new tunnel"
        echo "  stop      Stop an existing tunnel by local port"
        echo "  restart   Restart an existing tunnel by local port"
        echo "  status    Show detailed status of a specific tunnel by local port"
        echo "  list      List all tunnels (both running and stopped)"
        echo
        echo "Options for 'start':"
        echo "  -t, --tunnel-type TYPE   Tunnel type (Allowed: ssh, cloudflare)"
        echo "  -n, --hostname HOSTNAME  Hostname to use for tunnel"
        echo "  -l, --local-port  PORT   Local port to listen on"
        echo "ssh specific options:"
        echo "  -H, --remote-host HOST   Remote host to connect to (Default: localhost)"
        echo "  -r, --remote-port PORT   Port on the remote host (Required, local port default to this port)"
        echo "  ... other ssh options are passed directly to ssh"
        echo
        echo "Options for 'stop', 'status', 'restart':"
        echo " PORT Local port of the tunnel"
        echo
        echo "Examples:"
        echo "  tunnel start -t ssh -r 1234 -n username@example.com"
        echo "  tunnel start -t ssh -H db.example.com -r 5432 -l 15432 -n user@example.com"
        echo "  tunnel start -t cloudflare -l 15432 -n server.example.com"
        echo "  tunnel list           # List all tunnels"
        echo "  tunnel status 8080    # Show details for the tunnel using local port 8080"
        echo "  tunnel restart 8080   # Restart the tunnel using local port 8080"
        echo "  tunnel stop 8080      # Stop the tunnel using local port 8080"
    end >&2
end

# Main function to dispatch commands
function main
    # Check if a command was provided
    if test (count $argv) -lt 1
        usage
        exit 1
    end

    # Get command and remove it from argv
    set command $argv[1]
    set -e argv[1]

    switch $command
        case start
            cmd_start $argv
        case stop
            cmd_stop $argv
        case list
            cmd_list $argv
        case status
            cmd_status $argv
        case restart
            cmd_restart $argv
        case help -h --help
            usage
            exit 0
        case "*"
            echo "Unknown command: $command" >&2
            usage
            exit 1
    end

    # Return the status of the command
    return $status
end

# Run the main function with all arguments
main $argv
